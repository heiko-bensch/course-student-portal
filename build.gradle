plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.netflix.nebula.release' version '19.0.10'
    id "com.google.cloud.tools.jib" version "3.4.3"
    id "com.github.spotbugs" version "6.0.24"
    id 'jacoco'
}

group = 'de.bensch'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    mapstructVersion = "1.6.2"
    poiVersion = "5.3.0"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.apache.commons:commons-lang3'

    implementation "org.apache.poi:poi:${poiVersion}"
    implementation "org.apache.poi:poi-ooxml:${poiVersion}"
    implementation 'org.dhatim:fastexcel-reader:0.18.1'
    implementation 'org.dhatim:fastexcel:0.18.1'

//    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation('org.webjars:bootstrap:5.3.3')
    implementation 'org.webjars:font-awesome:6.5.2'
    implementation('org.webjars:jquery:3.7.1')
    implementation 'org.webjars:webjars-locator-core'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    developmentOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'net.datafaker:datafaker:2.3.0'
}
tasks.named('test') {
    useJUnitPlatform()
}

def githubUsername = project.findProperty('githubUsername') ?: 'default_username'
def imageName = project.findProperty('imageName') ?: 'default_image'

jib {
    to {
        image = "ghcr.io/${githubUsername}/${imageName}"
        tags = [project.version.toString(), 'latest']
        container {
            jvmFlags = ['-Xms512m', '-Xdebug', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseContainerSupport']
            mainClass = 'de.bensch.course.CourseApplication'
            ports = ['8080']
        }
    }
}

spotbugs {
    maxHeapSize = "1g"
    ignoreFailures = false
    showStackTraces = true
    showProgress = true
}

spotbugsMain {
    reports.create("html") {
        required = true

        outputLocation = file("${layout.buildDirectory.get().asFile}/reports/spotbugs/spotbugs.html")
        setStylesheet("fancy-hist.xsl")
    }
    reports.create("xml") {
        required = true
        outputLocation = file("${layout.buildDirectory.get().asFile}/reports/spotbugs/spotbugs.xml")
    }
    excludeFilter = file('config/spotbugs/excludeFilter.xml')
}

jacocoTestReport {
    reports {
        xml.required = true
        xml.outputLocation = file("${layout.buildDirectory.get().asFile}/reports/jacoco/jacoco.xml")
        html.outputLocation = file("${layout.buildDirectory.get().asFile}/reports/jacoco/html")
    }
}


test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

